plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'com.palantir.git-version' version '3.1.0'
}

group = 'ru.panyukovnn'
version = gitVersion()

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // Picocli для CLI
    implementation 'info.picocli:picocli:4.7.5'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.5'

    // Логирование
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'org.slf4j:slf4j-api:2.0.9'

    // Утилиты
    implementation 'org.apache.commons:commons-lang3:3.20.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Тестирование
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation("org.hamcrest:hamcrest:3.0")
}

application {
    mainClass = 'ru.panyukovnn.ytsubtitlesloader.YtSubtitlesLoaderCli'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'ru.panyukovnn.ytsubtitlesloader.YtSubtitlesLoaderCli'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.getGroup()
            artifactId = rootProject.getName()
            version = rootProject.getVersion()

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/panyukovnn/yt-subtitles-starter"
            credentials {
                username = 'panyukovnn'
                password = "$githubPackagesWriteToken"
            }
        }
    }
}
